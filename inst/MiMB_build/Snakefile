# Snakefile for building MiMB (Methods in Molecular Biology) submission
#
# Location: inst/MiMB_build/
# Run from this directory: cd inst/MiMB_build && snakemake -j1 <target>
#
# Usage:
#   snakemake -j1              # Build all outputs
#   snakemake -j1 help         # Show this help
#   snakemake -j1 clean        # Remove generated files
#   snakemake -j1 <target>     # Build specific target
#
# Targets:
#   all                        - Build main submission (docx + ptmsea)
#   mea                        - Build 4 kinase/motif enrichment vignettes
#   render_supplementary       - Render MiMBIntegratedPTM.docx (dodea=false)
#   render_supplementary_full  - Render with full DEA (dodea=true, slow!)
#   render_supplementary_html  - Render MiMBIntegratedPTM.html
#   renumber_references        - Renumber Notes/Figures/Tables in DOCX
#   render_ptmsea              - Render Analysis_PTMSEA.html
#   render_motif               - Render Analysis_Motif_Enrichment.html
#   render_kl                  - Render Analysis_KinaseLibrary.html
#   render_vis                 - Render Vis_MEA.html
#   render_qc                  - Render QCReport.html with mouse example data
#   kl                         - Run kinase-library + render KL/Vis vignettes
#   install_package            - Reinstall prophosqua package
#   archive                    - Create dated submission zip (MiMB_submission_YYYYMMDD.zip)
#
# Notes:
#   - Package is automatically reinstalled when R/*.R or DESCRIPTION changes
#   - Use 'rm .package_installed' to force package reinstall
#   - Vignette sources are in ../../vignettes/
#   - Outputs are written to this directory (inst/MiMB_build/)

import glob
import datetime

# Path configuration (relative to inst/MiMB_build/)
PKG_ROOT = "../.."
VIGNETTES = f"{PKG_ROOT}/vignettes"
R_FILES = glob.glob(f"{PKG_ROOT}/R/*.R")
DESCRIPTION = f"{PKG_ROOT}/DESCRIPTION"
RDA_FILE = f"{PKG_ROOT}/data/combined_test_diff_example.rda"

CONTRASTS = ["KO_vs_WT", "KO_vs_WT_at_Early", "KO_vs_WT_at_Late", "KO_vs_WT_at_Uninfect"]
KL_REPO = "git+https://github.com/wolski/kinase-library"

rule help:
    """Show available targets and usage"""
    run:
        print("""
Snakefile for building MiMB submission
Location: inst/MiMB_build/

Usage:
  snakemake -j1              Build all outputs
  snakemake -j1 help         Show this help
  snakemake -j1 clean        Remove generated files
  snakemake -j1 <target>     Build specific target

Targets:
  all                        Build main submission (docx + ptmsea)
  mea                        Build 4 kinase/motif enrichment vignettes
  render_supplementary       Render MiMBIntegratedPTM.docx (dodea=false)
  render_supplementary_full  Render with full DEA (dodea=true, slow!)
  renumber_references        Renumber Notes/Figures/Tables in DOCX
  render_ptmsea              Render Analysis_PTMSEA.html
  render_motif               Render Analysis_Motif_Enrichment.html
  render_kl                  Render Analysis_KinaseLibrary.html
  render_vis                 Render Vis_MEA.html
  render_qc                  Render QCReport.html with mouse example data
  kl                         Run kinase-library + render KL/Vis vignettes
  install_package            Reinstall prophosqua package
  archive                    Create dated submission zip (MiMB_submission_YYYYMMDD.zip)

Notes:
  - Package is automatically reinstalled when R/*.R or DESCRIPTION changes
  - Use 'rm .package_installed' to force package reinstall
        """)

rule all:
    input:
        "MiMBIntegratedPTM_renumbered.docx",
        "Analysis_PTMSEA.html"

rule mea:
    """Build all 4 kinase/motif enrichment vignettes"""
    input:
        "Analysis_PTMSEA.html",
        "Analysis_Motif_Enrichment.html",
        "Analysis_KinaseLibrary.html",
        "Vis_MEA.html"

rule clean:
    """Remove generated files"""
    shell:
        """
        rm -f .package_installed
        rm -f MiMBIntegratedPTM.docx MiMBIntegratedPTM_renumbered.docx MiMBIntegratedPTM_full.docx
        rm -f MiMBIntegratedPTM.html MiMBIntegratedPTM.pdf
        rm -f Analysis_PTMSEA.html Analysis_Motif_Enrichment.html
        rm -f Analysis_KinaseLibrary.html Vis_MEA.html
        rm -f *.knit.md *.utf8.md
        rm -f seqwindows.tsv *.rnk term2gene.csv mea_*.csv
        rm -rf MiMBIntegratedPTM_files
        """

rule install_package:
    input:
        r_files=R_FILES,
        description=DESCRIPTION
    output:
        touch(".package_installed")
    shell:
        """
        Rscript -e "devtools::install('{PKG_ROOT}')"
        """

rule render_supplementary:
    input:
        rmd=f"{VIGNETTES}/MiMBIntegratedPTM.Rmd",
        pkg=".package_installed"
    output:
        "MiMBIntegratedPTM.docx"
    params:
        dodea="FALSE"
    shell:
        """
        Rscript -e "rmarkdown::render('{input.rmd}', output_format = 'bookdown::word_document2', output_dir = '.', params = list(dodea = {params.dodea}), clean = FALSE)"
        """

rule render_supplementary_full:
    """Render MiMBIntegratedPTM with full DEA (dodea=true)"""
    input:
        rmd=f"{VIGNETTES}/MiMBIntegratedPTM.Rmd",
        pkg=".package_installed"
    output:
        "MiMBIntegratedPTM_full.docx"
    shell:
        """
        Rscript -e "rmarkdown::render('{input.rmd}', output_format = 'bookdown::word_document2', output_dir = '.', output_file = 'MiMBIntegratedPTM_full.docx', params = list(dodea = TRUE), clean = FALSE)"
        """

rule render_supplementary_html:
    """Render MiMBIntegratedPTM as HTML (with full DEA)"""
    input:
        rmd=f"{VIGNETTES}/MiMBIntegratedPTM.Rmd",
        pkg=".package_installed"
    output:
        "MiMBIntegratedPTM.html"
    shell:
        """
        Rscript -e "rmarkdown::render('{input.rmd}', output_format = 'bookdown::html_document2', output_dir = '.', output_file = 'MiMBIntegratedPTM.html', params = list(dodea = TRUE), clean = FALSE)"
        """

rule renumber_references:
    """Renumber Note 4.X -> X, Figure 5.X -> X, Table 5.X -> X in DOCX"""
    input:
        docx="MiMBIntegratedPTM.docx",
        script="rename_notes.py"
    output:
        "MiMBIntegratedPTM_renumbered.docx"
    shell:
        """
        uv run --with python-docx python {input.script}
        """

rule render_supplementary_pdf:
    """Render MiMBIntegratedPTM as PDF"""
    input:
        rmd=f"{VIGNETTES}/MiMBIntegratedPTM.Rmd",
        pkg=".package_installed"
    output:
        "MiMBIntegratedPTM.pdf"
    shell:
        """
        Rscript -e "rmarkdown::render('{input.rmd}', output_format = 'bookdown::pdf_document2', output_dir = '.', params = list(dodea = FALSE), clean = FALSE)"
        """

rule archive:
    """Create dated submission zip for MiMB journal"""
    input:
        docx="MiMBIntegratedPTM_renumbered.docx",
        pdf="MiMBIntegratedPTM.pdf",
        bib=f"{VIGNETTES}/doi_references_key.bib",
        csl=f"{VIGNETTES}/springer-basic-brackets.csl"
    output:
        "MiMB_submission_{date}.zip".format(date=datetime.datetime.now().strftime("%Y%m%d"))
    params:
        dirname="MiMB_submission_{date}".format(date=datetime.datetime.now().strftime("%Y%m%d"))
    shell:
        """
        rm -rf {params.dirname}
        mkdir -p {params.dirname}
        cp {input.docx} {params.dirname}/
        cp {input.pdf} {params.dirname}/
        cp {input.bib} {params.dirname}/
        cp {input.csl} {params.dirname}/
        cp -r MiMBIntegratedPTM_files/figure-docx {params.dirname}/ 2>/dev/null || true
        cp -r MiMBIntegratedPTM_files/figure-latex {params.dirname}/ 2>/dev/null || true
        zip -r {output} {params.dirname}
        rm -rf {params.dirname}
        """

rule render_ptmsea:
    input:
        rmd=f"{VIGNETTES}/Analysis_PTMSEA.Rmd",
        pkg=".package_installed"
    output:
        "Analysis_PTMSEA.html"
    shell:
        """
        Rscript -e "rmarkdown::render('{input.rmd}', output_dir = '.', clean = FALSE)"
        """

rule render_motif:
    input:
        rmd=f"{VIGNETTES}/Analysis_Motif_Enrichment.Rmd",
        pkg=".package_installed"
    output:
        "Analysis_Motif_Enrichment.html"
    shell:
        """
        Rscript -e "rmarkdown::render('{input.rmd}', output_dir = '.', clean = FALSE)"
        """

rule render_kl:
    input:
        rmd=f"{VIGNETTES}/Analysis_KinaseLibrary.Rmd",
        pkg=".package_installed",
        term2gene="term2gene.csv"
    output:
        "Analysis_KinaseLibrary.html"
    shell:
        """
        Rscript -e "rmarkdown::render('{input.rmd}', output_dir = '.', clean = FALSE)"
        """

rule render_vis:
    input:
        rmd=f"{VIGNETTES}/Vis_MEA.Rmd",
        pkg=".package_installed",
        mea=expand("mea_{contrast}.csv", contrast=CONTRASTS)
    output:
        "Vis_MEA.html"
    shell:
        """
        Rscript -e "rmarkdown::render('{input.rmd}', output_dir = '.', clean = FALSE)"
        """

# Kinase-library integration rules

rule kl:
    """Run kinase-library analysis + render KL/Vis vignettes"""
    input:
        "term2gene.csv",
        expand("mea_{contrast}.csv", contrast=CONTRASTS),
        "Analysis_KinaseLibrary.html",
        "Vis_MEA.html"

rule extract_seqs:
    """Extract unique sequences from RDA for motif scanning"""
    input:
        RDA_FILE
    output:
        "seqwindows.tsv"
    shell:
        """
        Rscript -e "
        load('{input}')
        data <- combined_test_diff_example
        seqs <- unique(data[, 'SequenceWindow', drop=FALSE])
        write.table(seqs, '{output}', sep='\t', row.names=FALSE, quote=FALSE)
        "
        """

rule extract_rnk:
    """Extract ranked sequences per contrast for MEA"""
    input:
        RDA_FILE
    output:
        expand("{contrast}.rnk", contrast=CONTRASTS)
    shell:
        """
        Rscript -e "
        load('{input}')
        data <- combined_test_diff_example
        for (ct in unique(data\\$contrast)) {{
            sub <- data[data\\$contrast == ct, c('SequenceWindow', 'statistic.site')]
            sub <- sub[!is.na(sub\\$statistic.site), ]
            fname <- paste0(ct, '.rnk')
            write.table(sub, fname, sep='\t', row.names=FALSE, quote=FALSE)
        }}
        "
        """

rule scan_motifs:
    """Run kinase-library scan-motifs to generate term2gene.csv"""
    input:
        "seqwindows.tsv"
    output:
        "term2gene.csv"
    params:
        repo=KL_REPO
    shell:
        """
        uv tool run --from {params.repo} scan-motifs {input} \
            --seq-col SequenceWindow \
            --output {output} \
            --kin-type ser_thr \
            --threshold 95 \
            --term2gene
        """

rule run_mea:
    """Run kinase-library MEA for a single contrast"""
    input:
        "{contrast}.rnk"
    output:
        "mea_{contrast}.csv"
    threads: 4
    params:
        repo=KL_REPO
    shell:
        """
        uv tool run --from {params.repo} run-mea {input} \
            --seq-col SequenceWindow \
            --rank-col statistic.site \
            --output {output} \
            --kin-type ser_thr \
            --threshold 95 \
            --permutations 1000 \
            --threads {threads}
        """

# QC Report rule

rule render_qc:
    """Render QCReport with mouse example data"""
    input:
        qmd=f"{VIGNETTES}/QCReport.qmd",
        pkg=".package_installed",
        psm=f"{PKG_ROOT}/inst/PTM_example_analysis_v2/PTM_example/data_total/FP_22/p1/psm.tsv"
    output:
        f"{PKG_ROOT}/inst/PTM_example_analysis_v2/QCReport.html"
    params:
        dest=f"{PKG_ROOT}/inst/PTM_example_analysis_v2"
    shell:
        """
        cp {input.qmd} {params.dest}/
        cd {params.dest} && Rscript -e "rmarkdown::render('QCReport.qmd', output_format = 'html_document', params = list(wd = '.', psm = 'PTM_example/data_total/FP_22/p1/psm.tsv', fasta = 'PTM_example/data_total/FP_22/p37688_db3_MusNShigella_20250219.fasta', workunit = 'mouse_example', projectid = 'Maculins2020'))"
        """
